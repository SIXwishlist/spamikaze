#!/usr/bin/perl

# smtperror-exim
#
# Copyright (C) 2003 Hans Wolters <h-wolters@nl.linux.org>
# Copyright (C) 2003 Rik van Riel <riel@surriel.com>
# Copyright (C) 2004 Hans Spaans  <cj.spaans@nexit.nl>
# Released under the GNU GPL
#
# NO WARRANTY, see the file COPYING for details.
#
# This file is part of the spamikaze project:
#     http://spamikaze.nl.linux.org/
use strict;
use warnings;
use FindBin;
use Net::DNS;
use lib "$FindBin::Bin";
use File::Tail;

use Spamikaze;

sub storeip
{
    my ( $ip )  = @_;
    my $error   = 0;
    my @iplist  = split /\./, $ip;
    my $ts      = time();


    my $sql     = "INSERT INTO ipentries (id_ip, date_logged) SELECT 
                    ipnumbers.id, UNIX_TIMESTAMP() FROM ipnumbers WHERE 
                    octa = ? and octb = ? and octc = ? and octd = ?";

    my $visip   = "UPDATE ipnumbers SET visible = 1 WHERE
                        octa = ? AND octb = ? AND octc = ? AND octd = ?";
                        
    # Set the dbh from the new pm.

    my $dbh         = Spamikaze::DBConnect();

    unless ($#iplist == 3) {
    	$error = 1;
    }

    foreach my $num (@iplist) {
            $error = 1 if ($num < 0 or $num > 255 or $num =~ /[^\d]/);
    }

    if ($error < 1) {
        my $sth = $dbh->prepare( $sql );
        $sth->execute($iplist[0], $iplist[1], $iplist[2], $iplist[3]);
        $sth->finish();

        my $rv = $sth->rows;

        if ($rv < 1){

            # the ipnumber isn't known, store it and get the id to
            # store it into the ipentries table.

            my $sqlipnumber = "INSERT INTO ipnumbers (octa, octb, octc, octd)
                               VALUES ( ?, ?, ?, ?)";
                               
            my $sthipnumber = $dbh->prepare( $sqlipnumber );
            $sthipnumber->execute($iplist[0], $iplist[1], $iplist[2], $iplist[3]);
            $sthipnumber->finish();

            $sth = $dbh->prepare( $sql );
            $sth->execute($iplist[0], $iplist[1], $iplist[2], $iplist[3]);
            $sth->finish();
        }

        # needed to set all earlier entries for this ipnumber to visible.
        my $sthupdate = $dbh->prepare( $visip );
        $sthupdate->execute($iplist[0], $iplist[1], $iplist[2], $iplist[3]);
        $sthupdate->finish();
    }        
    $dbh->disconnect();

}

sub parselog
{
	my ( $line ) = @_;
	my $ip = '';

	if ($line =~ /SMTP protocol violation: synchronization error.*from H=\[((\d{1,3}\.){3}\d{1,3})\]/) {
		$ip = $1;
	} elsif ($line =~ /SMTP protocol violation: synchronization error.* \[((\d{1,3}\.){3}\d{1,3})\] next input=/) {
		$ip = $1;
	}

	return $ip;
}

sub main
{
	my $file = File::Tail->new("/var/log/exim/reject.log");
	my $line;

	while (defined($line=$file->read)) {
		my $ip = parselog($line);
		if (defined($ip) and $ip ne '') {
			storeip($ip);
		}
	}
}

&main;
